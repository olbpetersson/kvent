buildscript {
    repositories {
        mavenCentral()
    }

    ext.kotlin_version = '1.3.50'
    ext.ktor_version = '1.2.5'

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
    }
}

plugins {
    // Prints tests statuses during build
    id 'com.adarshr.test-logger' version '1.7.0'
    // Lists all available dependency upgrades. Use with ./gradlew dependencyUpdates -Drevision=release
    id 'com.github.ben-manes.versions' version '0.22.0'
}

// packages a fat jar
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'kotlin'

jar.enabled = false
tasks.build.dependsOn tasks.shadowJar

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }

repositories {
    mavenLocal()
    mavenCentral()
    maven { url  "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/kotlin/kotlinx" }
}

apply from: 'gradle/ktlint.gradle'

def appName = 'app'
// TODO: You may want to change the package name once you've cloned this repo
def mainClassName = 'se.olapetersson.ApplicationKt'

shadowJar {
    baseName = appName
    classifier = null
    version = null
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task run(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = mainClassName
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // ktor as http-server and http client
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-jackson:$ktor_version"
    compile "io.ktor:ktor-client-core:$ktor_version"
    compile "io.ktor:ktor-client-apache:$ktor_version"

    // Use logback + json encoder for logging
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile 'net.logstash.logback:logstash-logback-encoder:5.0'

    // Test dependencies
    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.13.2"
}
